// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    And16(a=x , b[0..15]=false , out=xzero);
    Mux16(a=x , b=xzero , sel=zx , out=xval);
    Not16(in=xval , out=notx);
    Mux16(a=xval , b=notx , sel=nx , out=xval2);


    And16(a=y , b[0..15]=false , out=yzero);
    Mux16(a=y , b=yzero , sel=zy , out=yval);
    Not16(in=yval , out=noty);
    Mux16(a=yval , b=noty , sel=ny , out=yval2);


    Add16(a=xval2, b=yval2, out=oval1);
    And16(a=xval2, b=yval2, out=oval2);
    Mux16(a=oval2, b=oval1, sel=f, out=oval3);

    Not16(in=oval3 , out=notoval3);
    Mux16(a=oval3, b=notoval3, sel=no, out=out, out[0..7]=out0to7, out[8..15]=out8to15, out[15]=ng);

    Or8Way(in=out0to7, out=lowOr);
    Or8Way(in=out8to15, out=highOr);
    Or(a=lowOr, b=highOr, out=outOr);
    Not(in=outOr, out=zr);

}